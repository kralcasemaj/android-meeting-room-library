plugins {
    id 'com.android.library'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'kotlin-parcelize'
}

def AAR_NAME = "API-LIB"
def DISTRIBUTION_VERSION_CODE = 1
def DISTRIBUTION_VERSION_NAME = "1.0.0"

android {
    compileSdk 32

    defaultConfig {
        minSdk 26
        targetSdk 32
        versionCode DISTRIBUTION_VERSION_CODE
        versionName DISTRIBUTION_VERSION_NAME

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        multiDexEnabled true
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    flavorDimensions 'tier'

    productFlavors {

        distribution {
            dimension 'tier'
            versionCode DISTRIBUTION_VERSION_CODE
            versionName DISTRIBUTION_VERSION_NAME
            setProperty("archivesBaseName", "$AAR_NAME-$versionName")
        }

        libraryVariants.all { variant ->
            variant.outputs.all {
                outputFileName = "$AAR_NAME-$DISTRIBUTION_VERSION_NAME" + ".aar"
            }
        }
    }

    compileOptions {
        coreLibraryDesugaringEnabled true
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }

    /* Copy the library .aar to the sample app libs directory after building the library */
    task copyAARToApp(type: Copy) {
        from file("$buildDir/outputs/aar/" + "$AAR_NAME-$DISTRIBUTION_VERSION_NAME" + ".aar")
        into file("$project.rootDir/app/libs")
    }
    afterEvaluate {
        assembleDistribution.finalizedBy(copyAARToApp)
    }
}

dependencies {

    implementation 'androidx.core:core-ktx:1.7.0'
    implementation 'androidx.appcompat:appcompat:1.4.1'
    implementation 'com.google.android.material:material:1.6.0'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.4.1'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.4.2'
    implementation 'com.google.code.gson:gson:2.8.9'
    implementation 'com.squareup.retrofit2:retrofit:2.9.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.9.0'
    implementation 'com.squareup.okhttp3:okhttp:4.9.0'
    implementation 'com.squareup.okhttp3:logging-interceptor:4.9.0'
    implementation "android.arch.lifecycle:extensions:1.1.1"

    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:1.1.5'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
}